EntryPoint:
    syscall OS_GETMOUSESWAP             ; Get mouse swap setting
    move.b  d7, d6
    moveq   #ID_MOUSESWAP, d7
    syscall OS_SETSTATE
    
    syscall OS_GETMOUSETYPE             ; Get mouse type setting
    move.b  d7, d6
    moveq   #ID_MOUSETYPE, d7
    syscall OS_SETSTATE
    
    syscall OS_GETMOUSESPEED            ; Get mouse sensivity
    move.b  d7, d6
    moveq   #ID_MOUSESENS, d7
    syscall OS_SETSTATE
    
    moveq   #0, d7                      ; Load graphics
    move.w  #NumPatterns, d6
    lea     Patterns(pc), a6
    syscall OS_LOADPATTERNS
    lea     LabelText(pc), a6
    move.w  #VramMouseButtons, d7
    syscall OS_LOADTEXTLIST
    
    lea     Layout(pc), a6              ; Load interface
    syscall OS_PARSELAYOUT
    lea     LabelList(pc), a6
    syscall OS_DRAWLABELS
    
    moveq   #0, d7                      ; To shift some labels a bit
    moveq   #-4, d6
    move.w  d7, a6
    move.w  d6, a5
    syscall OS_SETSCROLL

;----------------------------------------------------------------------------

MainLoop:
    syscall OS_GUILOOP                  ; Let the GUI run
    
    and.w   #$FF, d7                    ; Handle the changed setting
    lsl.w   #2, d7
    lea     @Handlers(pc), a6
    jsr     (a6,d7.w)
    
    bra.s   MainLoop

;----------------------------------------------------------------------------

@Handlers:
    bra.w   SetMouseSwap                ; ID_MOUSESWAP
    bra.w   SetMouseType                ; ID_MOUSETYPE
    bra.w   SetSensivity                ; ID_MOUSESENS
    bra.w   DecreaseSensivity           ; ID_MOUSESENS_D
    bra.w   IncreaseSensivity           ; ID_MOUSESENS_U
    illegal                             ; ID_TEST

@Dummy:
    rts

;****************************************************************************
; SetMouseSwap
; Changes the mouse swap setting.
;----------------------------------------------------------------------------
; input d6.b ... 0 = right handed, 1 = left handed
;----------------------------------------------------------------------------
; breaks: all
;****************************************************************************

SetMouseSwap:
    move.b  d6, d7
    syscall OS_SETMOUSESWAP
    rts

;****************************************************************************
; SetMouseType
; Changes the mouse type.
;----------------------------------------------------------------------------
; input d6.b ... 0 = Mega Mouse, 1 = Sega Mouse
;----------------------------------------------------------------------------
; breaks: all
;****************************************************************************

SetMouseType:
    move.b  d6, d7
    syscall OS_SETMOUSETYPE
    rts

;****************************************************************************
; SetSensivity
; Updates the mouse sensivity immediately.
;----------------------------------------------------------------------------
; input d6.b ... New sensivity
;----------------------------------------------------------------------------
; breaks: all
;****************************************************************************

SetSensivity:
    move.b  d6, d7                      ; Update mouse sensivity
    syscall OS_SETMOUSESPEED
    
    rts                                 ; End of subroutine

;****************************************************************************
; DecreaseSensivity
; Decreases the mouse sensivity.
;----------------------------------------------------------------------------
; breaks: all
;****************************************************************************

DecreaseSensivity:
    moveq   #ID_MOUSESENS, d7           ; Get current sensivity
    syscall OS_GETSTATE
    
    subq.w  #1, d7                      ; Decrease it a bit
    bge.s   @ValueOk                    ; Make sure to not go too far!
    moveq   #0, d7
@ValueOk:
    move.b  d7, d0
    
    move.b  d0, d7                      ; Change mouse sensivity
    syscall OS_SETMOUSESPEED
    
    move.b  d0, d6                      ; Store new sensivity
    moveq   #ID_MOUSESENS, d7
    syscall OS_SETSTATE
    
    rts                                 ; End of subroutine

;****************************************************************************
; IncreaseSensivity
; Increases the mouse sensivity.
;----------------------------------------------------------------------------
; breaks: all
;****************************************************************************

IncreaseSensivity:
    moveq   #ID_MOUSESENS, d7           ; Get current sensivity
    syscall OS_GETSTATE
    
    addq.w  #1, d7                      ; Increase it a bit
    cmp.w   #$40, d7                    ; Make sure to not go too far!
    blo.s   @ValueOk
    moveq   #$40, d7
@ValueOk:
    move.b  d7, d0
    
    move.b  d0, d7                      ; Change mouse sensivity
    syscall OS_SETMOUSESPEED
    
    move.b  d0, d6                      ; Store new sensivity
    moveq   #ID_MOUSESENS, d7
    syscall OS_SETSTATE
    
    rts                                 ; End of subroutine
